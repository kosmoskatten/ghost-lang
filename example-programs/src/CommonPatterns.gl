/*
 * A collection of common ghost-lang patterns.
 */
module Main

// Import the generic Web.Page module
import Web.Page

// Import the generic Web.Media module
import Web.Media

/*
 * A pattern that is describing a behavior for browsing www news paper
 * pages. The behavior is starting by loading a frontpage, and later
 * following a few links from the frontpage.
 */
pattern newsPageReader with weight 1
{
  // Load the frontpage, which is containing html, external js,
  // external css and a parameterized number of images of a
  // parameterized size in KB.
  webPage_loadNewsPaperPage(gaussian(50, 10), gaussian(30, 12)),

  // Follow a few links. Those pages typically contain fewer but
  // larger images.
  loop gaussian(3, 1)
  {
    Delay gaussian(20, 10) sec,
    webPage_loadNewsPaperPage(gaussian(12, 4), gaussian(80, 22))
  }
}

/*
 * A pattern that is describing a behavior for viewing short video
 * clips. The behavior is started by loading a frontpage, and later
 * viewing a few short clips form the frontpage.
 */
pattern shortVideoClipViewer with weight 1
{
  // Load the frontpage, which is containing html, external js,
  // external css and a parameterized number of images.
  webPage_loadNewsPaperPage(gaussian(30, 6), gaussian(80, 22)),

  // Stream a buffer paced video stream. The size of the stream is in
  // MB and the pace is in mpbs.
  webMedia_streamVideoPaced(gaussian(25, 3), literal(10))
}
